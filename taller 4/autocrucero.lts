set Sensors = {engineOn,engineOff,on,off,resume,brake,accelerator} 
set Engine  = {engineOn,engineOff} 
set Prompts = {clearSpeed,recordSpeed,enableControl,disableControl} 
		
set Alphabet = {accelerator,brake,clearSpeed,disableControl,	
         enableControl,engineOff,engineOn,off,on,			
         recordSpeed,resume,setThrottle,speed,zoom}
			
SENSORSCAN = ({Sensors} -> SENSORSCAN).
			
INPUTSPEED = (engineOn -> CHECKSPEED),
CHECKSPEED = (speed -> CHECKSPEED |engineOff -> INPUTSPEED).
			
THROTTLE =(setThrottle -> zoom -> THROTTLE).
			
SPEEDCONTROL = DISABLED,
DISABLED =({speed,clearSpeed,recordSpeed}->DISABLED
            | enableControl -> ENABLED ), 
ENABLED = ( speed -> setThrottle -> ENABLED 
            |{recordSpeed,enableControl} -> ENABLED
            | disableControl -> DISABLED ).
			
set DisableActions = {off,brake,accelerator}
			
CRUISECONTROLLER = INACTIVE,
INACTIVE = (engineOn -> clearSpeed -> ACTIVE
            |DisableActions -> INACTIVE ),
ACTIVE   =(engineOff -> INACTIVE
            |on->recordSpeed->enableControl->CRUISING
            |DisableActions -> ACTIVE ),
CRUISING =(engineOff -> INACTIVE
            |DisableActions->disableControl->STANDBY
            |on->recordSpeed->enableControl->CRUISING ),
STANDBY  =(engineOff -> INACTIVE
            |resume -> enableControl -> CRUISING
            |on->recordSpeed->enableControl->CRUISING
            |DisableActions -> STANDBY ).
					
||CONTROL = (CRUISECONTROLLER||SPEEDCONTROL).
			
||CRUISECONTROLSYSTEM = (CONTROL||SENSORSCAN||INPUTSPEED||THROTTLE).

SENSORSCAN2 = (engineOn -> accelerator -> accelerator -> 
			on -> accelerator -> STOP).
			
CRUISECONTROLLER2 = (engineOn -> clearSpeed -> accelerator -> accelerator -> 
			on ->  recordSpeed -> enableControl -> accelerator -> disableControl -> ERROR).

SPEEDCONTROLLER2 = (clearSpeed -> recordSpeed -> enableControl -> speed -> 
					setThrottle -> speed -> setThrottle -> speed -> disableControl -> ERROR).
INPUTSPEED2 = (speed -> speed -> speed -> STOP).
THROTTLE2 = (setThrottle -> zoom -> setThrottle -> zoom -> STOP).

||CRUISECONTROLSYSTEM2 = (CRUISECONTROLLER2||SPEEDCONTROLLER2||SENSORSCAN2||INPUTSPEED2||THROTTLE2).

||CHECK = (CRUISECONTROLSYSTEM || CRUISECONTROLSYSTEM2).
